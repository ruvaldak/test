plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

architectury {
    platformSetupLoomIde()
    fabric()
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

loom {
    accessWidener = file("src/main/resources/roughlyenoughitems.accessWidener")
}

dependencies {
    modApi("net.fabricmc:fabric-loader:${project.fabricloader_version}")
    modRuntime("net.fabricmc.fabric-api:fabric-api:${project.fabric_api}")
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(module: "fabric-api")
    }
    modRuntime("com.terraformersmc:modmenu:${modmenu_version}") {
        transitive(false)
    }
    modRuntime("me.shedaniel:SmoothScrollingEverywhere:3.0.3-unstable") {
        transitive(false)
    }
    modApi("me.shedaniel:architectury-fabric:${architectury_version}")

    implementation(project(path: ":api")) { transitive = false }
    implementation(project(path: ":default-plugin")) { transitive = false }
    implementation(project(path: ":runtime")) { transitive = false }
    developmentFabric(project(path: ":api")) { transitive = false }
    developmentFabric(project(path: ":default-plugin")) { transitive = false }
    developmentFabric(project(path: ":runtime")) { transitive = false }

    shadowCommon(project(path: ":api", configuration: "transformProductionFabric")) { transitive = false }
    shadowCommon(project(path: ":default-plugin", configuration: "transformProductionFabric")) { transitive = false }
    shadowCommon(project(path: ":runtime", configuration: "transformProductionFabric")) { transitive = false }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}
